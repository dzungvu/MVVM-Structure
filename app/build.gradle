apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply from: '../dependencies.gradle'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId MetaData.application_id
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode version_code
        versionName Versions.version_name

        //application binary interface
        ndk.abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        setProperty("archivesBaseName", MetaData.apk_name)
    }

    flavorDimensions "mode"

    productFlavors {
        dev {
            manifestPlaceholders = [appName: MetaData.app_name_dev]
        }

        product {
            manifestPlaceholders = [appName: MetaData.app_name]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", MetaData.app_name

            //Prevent compiler to create unnecessary java bytecode
            kotlinOptions {
                freeCompilerArgs = [
                        '-Xno-param-assertions',
                        '-Xno-call-assertions',
                        '-Xno-receiver-assertions'
                ]
            }
        }

        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-debug.pro'
            resValue "string", "app_name", MetaData.app_name
            manifestPlaceholders = [appNameSuffix: " Debug"]
            applicationIdSuffix = ".debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    androidExtensions {
        experimental = true
    }

    buildFeatures {
        viewBinding true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    ndkVersion '21.1.6352462'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation Deps.app_compat
    implementation Deps.app_compat_resources
    implementation Deps.multidex

    implementation Deps.constraint_layout
    implementation Deps.android_material
    implementation Deps.common_codec

    //KTX
    implementation KTX.core
    implementation KTX.fragment

    //Hilt
    implementation Hilt.android
    kapt Hilt.android_compiler
    implementation Hilt.lifecycle_viewmodel
    kapt Hilt.compiler

    //Room
    implementation Room.ktx
    kapt Room.complier

    //Kotlin
    implementation Kotlin.stdlib

    //Coroutines
    implementation Coroutines.core
    implementation Coroutines.android

    //MVVM with Lifecycle
    implementation Lifecycle.extensions
    implementation Lifecycle.viewmodel
    implementation Lifecycle.livedata_ktx

    //Retrofit
    implementation Retrofit.core
    implementation Retrofit.converter
    implementation Retrofit.logging_interceptor

    implementation Navigation.fragment_ktx
    implementation Navigation.ui_ktx

    //Load image
    implementation Glide.glide
    kapt Glide.compiler


    implementation Deps.security_crypto

    //Test implement
    testImplementation 'junit:junit:4.13'
    androidTestImplementation TestDeps.runner
    androidTestImplementation TestDeps.espresso_core

}